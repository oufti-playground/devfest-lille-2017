= Continuous Delivery with Jenkins

aka "CD"

== CD: Why ?

[quote]
____
How long would it take your organization to deploy a change that
involves just one single line of code?
____

* Reduce deployment risks
* Allow more frequent user feedback
* Make progress believable by everyone

== CD: What ?

Continuous Delivery is the next step after Continuous Integration:

* Every change to the system *can* be released for *production*
* Delivery can be done at *any* time, on *any* environment

[quote, Martin Fowler]
____
Your team prioritizes keeping the software *deployable* over working on new features
____

== CD is NOT Continuous Deployment

Both are always confused:

image::{imagedir}/continuous-depl-vs-delivery.jpg[width=600]


== CD: How ?

* Having a collaborating working relationship with *everyone* involved
* Using *Deployment Pipelines*, which are automated implementations of
your applicationâ€™s build lifecycle process

== CD: Delivery Target

* Production runs on Docker
* Your Ops team use a Docker Registry
* Expected Artifact:
** Not a `jar` file
** But a Docker image

== CD: Exercise - Docker Test Suite

* Goal: Run the *Docker Test Suite*
** Using a single stage named "Docker",
*before* Integration Tests
** Using the agent labelled `docker`
** *Challenge:* we need the `jar` file at "Docker time"
** We do not need to archive artifact at the end,
unless Integration Test is unstable

* _Clues:_ Keywords `stash` and `unstash`

== CD: Solution - Docker Test Suite

[source,subs="attributes",groovy]
----
pipeline {
  agent { node { label 'maven' }}
  stages {
    stage('Build') {
      steps {
        sh 'mvn package'
        stash(name: 'app', includes: 'target/demoapp.jar')
      }
      post { always { junit '**/target/surefire-reports/*.xml' }}
    }
    stage('Docker') {
      agent {
        label 'docker'
      }
      steps {
        unstash 'app'
        sh '/usr/local/bin/bats ./src/test/bats/docker.bats'
      }
    }
    stage('Verify') {
      steps {
        sh 'mvn verify -fn'
        junit '**/target/failsafe-reports/*.xml'
      }
      post { unstable { archiveArtifacts 'target/demoapp.jar' }}
    }
  }
}
----

== CD: Exercise - Approval and Delivery

* Goal: We want a Human Approval *before* Delivery
* Add 2 stages named `Approval` and `Delivery`:
** `Approval` will ask for a manual validation,
after Integration Tests
** `Delivery` will tag and push the Docker Image
to the Docker registry at the URL `localhost:5000`

* _Clues:_ Keyword `input`

== CD: Solution - Approval and Delivery

[source,subs="attributes",groovy]
----
pipeline {
  agent { node { label 'maven' }}
  stages {
    stage('Build') {
      steps { sh 'mvn package'
        stash(name: 'app', includes: 'target/demoapp.jar') }
      post { always { junit '**/target/surefire-reports/*.xml' }}
    }
    stage('Docker') {
      agent { label 'docker' }
      steps { unstash 'app'
        sh '/usr/local/bin/bats ./src/test/bats/docker.bats' }
    }
    stage('Verify') {
      steps { sh 'mvn verify -fn'
        junit '**/target/failsafe-reports/*.xml' }
      post { unstable { archiveArtifacts 'target/demoapp.jar' }}
    }
    stage('Approval') {
      agent none
      steps { input 'Is it OK to deploy demoapp ?' }
    }
    stage('Deploy') {
      agent { label 'docker' }
      steps {
        sh 'docker tag demoapp localhost:5000/demoapp:latest'
        sh 'docker push localhost:5000/demoapp:latest'
      }
    }
  }
}
----


== CD: Exercise - Building with Docker

* Goal: Use Docker to provide the build environment
** Use the agent allocation to build and run builds within a Docker container
** Use the `Dockerfile.build` from the repository

* _Clues:_ Keywords `agent none`, `agent { dockerfile ... label ...}`

== CD: Solution - Building with Docker

[source,subs="attributes",groovy]
----
pipeline { agent none
  stages {
    stage('Build') {
      agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'}}
      steps { sh 'mvn package'
        stash(name: 'app', includes: 'target/demoapp.jar') }
      post { always { junit '**/target/surefire-reports/*.xml' }}
    }
    stage('Docker') {
      agent { label 'docker' }
      steps { unstash 'app'
        sh '/usr/local/bin/bats ./src/test/bats/docker.bats' }
    }
    stage('Verify') {
      agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'}}
      steps { sh 'mvn verify -fn'
        junit '**/target/failsafe-reports/*.xml' }
      post { unstable { archiveArtifacts 'target/demoapp.jar' }}
    }
    stage('Approval') {
      agent none
      steps { input 'Is it OK to deploy demoapp ?' }
    }
    stage('Deploy') {
      agent { label 'docker' }
      steps { sh 'docker tag demoapp localhost:5000/demoapp:latest'
        sh 'docker push localhost:5000/demoapp:latest' }
    }
  }
}
----

== CD: Exercise - Scaling Pipeline

* *Goal:* Share Pipeline across your teams
* We want to use
*link:https://jenkins.io/doc/book/pipeline/shared-libraries/[Shared Libraries]*
* There is one autoconfigured named `deploy`
* Use the annotation to load the Library, on master branch
* Check the library
link:https://github.com/oufti-playground/example-voting-app/blob/master/vars/deploy.groovy[here]

* _Clues:_ Keywords `@Library`, `script`

== CD: Solution - Scaling Pipeline

[source,subs="attributes",groovy]
----
@Library('deploy@master') _
pipeline { agent none
  stages {
    stage('Build') {
      agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'}}
      steps { sh 'mvn package'
        stash(name: 'app', includes: 'target/demoapp.jar') }
      post { always { junit '**/target/surefire-reports/*.xml' }}
    }
    stage('Docker') {
      agent { label 'docker' }
      steps { unstash 'app'
        sh '/usr/local/bin/bats ./src/test/bats/docker.bats' }
    }
    stage('Verify') {
      agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'}}
      steps { sh 'mvn verify -fn'
        junit '**/target/failsafe-reports/*.xml' }
      post { unstable { archiveArtifacts 'target/demoapp.jar' }}
    }
    stage('Deploy') {
      agent none
      steps {
        script {
          deploy 'demoapp'
        }
      }
    }
  }
}
----


== CD: Exercise - Parallel Stages

* Goal: Run Stages in parallels to gain time
** We can safely run Docker Smoke and Integration Tests in parallel
** To specify a specific agent, use Scripted Pipeline Block
and the node allocation

* _Clues:_ Keywords `parallel`, `script`, `node`

* *WARNING:* link:https://issues.jenkins-ci.org/browse/JENKINS-41334[]
** I'm cheating in the solution below

== CD: Solution - Parallel Stages

[source,subs="attributes",groovy]
----
@Library('deploy@master') _
pipeline { agent none
  stages {
    stage('Build') { agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'}}
      steps { sh 'mvn package'
        stash(name: 'app', includes: 'target/demoapp.jar') }
      post { always { junit '**/target/surefire-reports/*.xml' }}
    }
    stage('Tests') { agent { dockerfile { filename 'Dockerfile.build'
        label 'docker'
        args '--network=docker_default'}}
      environment { DOCKER_HOST='tcp://docker-service:2375' }
      steps { parallel (
        "Integration Tests": {
          sh 'mvn verify -fn'
          junit '**/target/failsafe-reports/*.xml'
        },
        "Docker": {
          unstash 'app'
          sh 'bats ./src/test/bats/docker.bats'
        }
      )}
      post { unstable { archiveArtifacts 'target/demoapp.jar' }}
    }
    stage('Deploy') {
      agent none
      steps { script { deploy 'demoapp' }}
    }
  }
}
----

== That's all folks !
